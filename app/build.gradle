apply plugin: 'com.android.application'

Properties localProperties = new Properties()
localProperties.load(new FileInputStream("local.properties"))

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "rs.elfak.bobans.carsharing"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file("../signing/release.jks")
            storePassword rootProject.storePassword
            keyAlias rootProject.keyAlias
            keyPassword rootProject.keyPassword
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
            buildConfigField 'String', 'DEVELOPER_CREDENTIALS', '"' + localProperties.getProperty('developer.credentials') + '"'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'DEVELOPER_CREDENTIALS', '""'
        }
    }
    flavorDimensions "carSharing"
    productFlavors {
        local {
            dimension "carSharing"
            buildConfigField 'String', 'BASE_URL', '"http://192.168.1.12:8080/"'
        }
        heroku {
            dimension "carSharing"
            buildConfigField 'String', 'BASE_URL', '"https://car-sharing.herokuapp.com/"'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'

    // android support
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibVersion"
    implementation "com.android.support:design:$rootProject.supportLibVersion"

    // mosby
    implementation "com.hannesdorfmann.mosby:mvp:$rootProject.mosbyVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava:$rootProject.retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttp3Version"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttp3Version"

    //RxAndroid
    implementation "io.reactivex:rxandroid:$rootProject.rxAndroidVersion"
    implementation "io.reactivex:rxjava:$rootProject.rxJavaVersion"

    // Joda Time
    implementation "joda-time:joda-time:$rootProject.jodaTimeVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    // Glide transformations
    implementation "jp.wasabeef:glide-transformations:$rootProject.glideTransformationsVersion"

    // Butter Knife
    implementation "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"

    // Firebase
    implementation "com.google.firebase:firebase-messaging:$rootProject.firebaseVersion"
}

apply plugin: 'com.google.gms.google-services'