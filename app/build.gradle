apply plugin: 'com.android.application'
apply plugin: 'android-apt'

Properties localProperties = new Properties()
localProperties.load(new FileInputStream("local.properties"))

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "rs.elfak.bobans.carsharing"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file("../signing/release.jks")
            storePassword rootProject.storePassword
            keyAlias rootProject.keyAlias
            keyPassword rootProject.keyPassword
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
            buildConfigField 'String', 'DEVELOPER_CREDENTIALS', '"' + localProperties.getProperty('developer.credentials') + '"'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'DEVELOPER_CREDENTIALS', '""'
        }
    }
    productFlavors {
        local {
            buildConfigField 'String', 'BASE_URL', '"http://192.168.1.50:8080/"'
        }
        heroku {
            buildConfigField 'String', 'BASE_URL', '"https://car-sharing.herokuapp.com/"'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'

    // android support
    compile "com.android.support:appcompat-v7:$rootProject.supportLibVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibVersion"
    compile "com.android.support:design:$rootProject.supportLibVersion"

    // mosby
    compile "com.hannesdorfmann.mosby:mvp:$rootProject.mosbyVersion"

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$rootProject.retrofitVersion"
    compile "com.squareup.okhttp3:okhttp:$rootProject.okhttp3Version"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttp3Version"

    //RxAndroid
    compile "io.reactivex:rxandroid:$rootProject.rxAndroidVersion"
    compile "io.reactivex:rxjava:$rootProject.rxJavaVersion"

    // Joda Time
    compile "joda-time:joda-time:$rootProject.jodaTimeVersion"

    // Glide
    compile "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    // Glide transformations
    compile "jp.wasabeef:glide-transformations:$rootProject.glideTransformationsVersion"

    // Butter Knife
    compile "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    apt "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"

    // Firebase
    compile "com.google.firebase:firebase-messaging:$rootProject.firebaseVersion"
}

apply plugin: 'com.google.gms.google-services'